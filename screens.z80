; FHBG GB
; Copyright (C) 2018 NovaSquirrel
;
; This program is free software: you can redistribute it and/or
; modify it under the terms of the GNU General Public License as
; published by the Free Software Foundation; either version 3 of the
; License, or (at your option) any later version.
;
; This program is distributed in the hope that it will be useful, but
; WITHOUT ANY WARRANTY; without even the implied warranty of
; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
; General Public License for more details.
;
; You should have received a copy of the GNU General Public License
; along with this program.  If not, see <http://www.gnu.org/licenses/>.
;

SECTION "screens", ROM0

ShowTitle:
  ; Decompress title level
  ld de, title
  call DecompressMap
  call RenderLevel
  call UploadLevelPalette

  ; "Forehead"
  ld hl, TitleText1
  ld de, SCREEN1+2+6*32
  call strcpy
  ; "Block Guy"
  ld hl, TitleText2
  ld de, SCREEN1+3+7*32
  call strcpy

  ; On the Game Boy Color, invert the title
  ldh a, [IsGameBoyColor]
  cp $11
  jr nz, .NotColor
  vram_bank_1
  ; Make the bottom row inverted
  ld hl, SCREEN1+2+6*32
  ld a, 7
  ld c, 10
  call memset8
  ld hl, SCREEN1+2+7*32
  ld c, 10
  call memset8
  vram_bank_0
.NotColor:

  call ClearOAM
  call RunPlayer

  ; Clear sprites
  ld a, sprites>>8
  call run_dma

  call ScreenOn

  ; Wait for player to press A
.loop:
  call wait_vblank
  ; Copy player sprite
  ld a, sprites>>8
  call run_dma

  ; Run the player routine without allowing any key presses
  call ClearOAM
  xor a
  ldh [KeyDown], a
  ldh [KeyNew], a
  call RunPlayer

  call readkeys
  ldh a, [KeyNew]
  and KEY_A
  jr z, .loop

  call ScreenOff

  call ClearOAM

  jp ShowMainMenu

TitleText1:
  db "Forehead",0
TitleText2:
  db "Block Guy",0
PressStart:
  db "Push Start!",0

; ---------------------------------------------------

ShowMainMenuFromEditor:
  call wait_vblank
  call ScreenOff
ShowMainMenu:
  ; Reset edit mode, for if we go here from the editor
  xor a
  ldh [IsEditMode], a

  ; Clear the nametable
  call ClearAndAddFHBGText
  ld hl, SCREEN1
  ld b, 9
.clear_loop:
  ld d, 0
  call .side_border
  ld d, 2
  call .side_border
  dec b
  jr nz, .clear_loop
  
  xor a
  ldh [MenuCursor], a

  call ClearAndWriteOAM

  call UploadTitlePalette

  ldh a, [IsGameBoyColor]
  cp $11
  jr nz, .NotColor
  vram_bank_1
  ; Clear attributes
  ld hl, SCREEN1
  ld bc, 1024
  call memclear

  ; Set color 1 on sides
  ld b, 18
  ld hl, SCREEN1
.attribute_side_loop:
  ld a, 1
  ld [hl+], a
  ld [hl+], a
  ld a, L
  add 16
  ld L, a
  ld a, 1
  ld [hl+], a
  ld [hl], a
  call .side_border_increment2

  dec b
  jr nz, .attribute_side_loop

  vram_bank_0
.NotColor:

  ; Copy the palette in
  ld hl, .side_palette
  ld de, temp
  ld c, 4*2
  call memcpy8
  call .color_cycle

  ; Write the four options
  ld hl, PlayGameText
  ld de, SCREEN1+5+5*32
  call strcpy
  ld hl, HelpGameText
  ld de, SCREEN1+5+7*32
  call strcpy
  ld hl, EditGameText
  ld de, SCREEN1+5+9*32
  call strcpy
  ld hl, CreditsText
  ld de, SCREEN1+5+11*32
  call strcpy

  call ScreenOn

; --------------- Main menu loop -----------------
  ; Reset retraces
  xor a
  ldh [retraces], a
  ldh [LevelNumber], a
.loop:
  call wait_vblank
  ; Draw menu cursor
  ld a, $20    ; star
  ld [$fe02],a ; set tile number

  ; Draw the cursor sprite
  ldh a, [MenuCursor]
  add a ; * 16
  add a
  add a
  add a
  add (5*8)+16
  ld [$fe00], a ; Y position
  ld a, 8*4+8
  ld [$fe01], a ; X position

  ; Do color cycling?
  ldh a, [retraces]
  and 31
  call z, .color_cycle

  ; Read and respond to keys
  call readkeys
  ldh a, [KeyNew]
  and KEY_UP
  jr z, .not_up
    ldh a, [MenuCursor]
    dec a
    and 3
    ldh [MenuCursor], a
.not_up:

  ldh a, [KeyNew]
  and KEY_DOWN
  jr z, .not_down
    ldh a, [MenuCursor]
    inc a
    and 3
    ldh [MenuCursor], a
.not_down:

  ldh a, [KeyNew]
  and KEY_A
  jr z, .loop

  call ScreenOff

  call ClearAndWriteOAM

  ; Make a decision and jump
  ldh a, [MenuCursor]
  or a
  jp z, ShowPreLevel
  dec a
  jp z, ShowHelp
  dec a
  jp z, ShowEditor
  jp ShowCredits

; Paint side borders with tiles D and D+1
.side_border:
  ld [hl], d
  inc l
  ld [hl], d
  inc [hl]
  ld a, L
  add 17
  ld L, A

  ld [hl], d
  inc l
  ld [hl], d
  inc [hl]
; Reused for painting the attributes on
.side_border_increment2:
  ld a, L
  add 32-19
  ld L, a
  ret nc
  inc h
  ret

; Color cycling palette
.side_palette:
  RGB_Color 31, 31, 31
  RGB_Color (254)/8, (127)/8, (210)/8
  RGB_Color (134)/8, (244)/8, (98)/8
  RGB_Color (130)/8, (128)/8, (255)/8

; Write the colors and cycle to the next set
.color_cycle:
  ; Write those colors
  ld a, $80|4*2
  ldh [$ff68], a ; background palette index
  ld hl, temp
  ld c, 8
.cycle_loop:
  ld a, [hl+]
  ldh [$ff69], a
  dec c
  jr nz, .cycle_loop

  ; Cycle the colors around
  ldh a, [temp+3]
  push af
  ldh a, [temp+2]
  push af
  ldh a, [temp+4]
  ldh [temp+2], a
  ldh a, [temp+5]
  ldh [temp+3], a
  ldh a, [temp+6]
  ldh [temp+4], a
  ldh a, [temp+7]
  ldh [temp+5], a
  pop af
  ldh [temp+6], a
  pop af
  ldh [temp+7], a
  ret

PlayGameText:
  db "Start game!",0
HelpGameText:
  db "How to play",0
EditGameText:
  db "Level edit",0
CreditsText:
  db "Credits",0
ExtrasText:
  db "Extras",0

; ---------------------------------------------------

ShowCredits:
  call ClearAndAddFHBGText

  ld hl, CreditsText1
  ld de, SCREEN1+2+5*32
  call strcpy
  ld hl, CreditsText2
  ld de, SCREEN1+2+6*32
  call strcpy
  ld hl, CreditsText3
  ld de, SCREEN1+2+7*32
  call strcpy
  ld hl, CreditsText4
  ld de, SCREEN1+2+9*32
  call strcpy
  ld hl, CreditsText5
  ld de, SCREEN1+2+10*32
  call strcpy
  ld hl, CreditsText6
  ld de, SCREEN1+2+11*32
  call strcpy


.wait_for_a:
  ld hl, PressAToContinue
  ld de, SCREEN1+5+16*32
  call strcpy

  call ScreenOn

  call wait_vblank
  call readkeys
  ldh a, [KeyNew]
  and KEY_A|KEY_B
  jr z, .wait_for_a

  call ScreenOff

  jp ShowMainMenu

CreditsText1:
  db "Game",0
CreditsText2:
  db "by NovaSquirrel",0
CreditsText3:
  db "novasquirrel.com",0

CreditsText4:
  db "Enemy sprites",0
CreditsText5:
  db "by PinoBatch",0
CreditsText6:
  db "pineight.com",0

PressAToContinue:
  db "(Press A)",0

; ---------------------------------------------------

ShowHelp:
  call ClearAndAddFHBGText

  ld hl, HelpText1
  ld de, SCREEN1+2+4*32
  call strcpy
  ld hl, HelpText2
  ld de, SCREEN1+2+5*32
  call strcpy
  ld hl, HelpText3
  ld de, SCREEN1+2+6*32
  call strcpy
  ld hl, HelpText4
  ld de, SCREEN1+2+7*32
  call strcpy
  ld hl, HelpText5
  ld de, SCREEN1+2+8*32
  call strcpy
  ld hl, HelpText6
  ld de, SCREEN1+2+10*32
  call strcpy
  ld hl, HelpText7
  ld de, SCREEN1+2+11*32
  call strcpy
  ld hl, HelpText8
  ld de, SCREEN1+2+13*32
  call strcpy
  ld hl, HelpText9
  ld de, SCREEN1+2+14*32
  call strcpy

  jp ShowCredits.wait_for_a

HelpText1:
  db "Throw blocks at",0
HelpText2:
  db "your enemies to",0
HelpText3:
  db "stun them, then",0
HelpText4:
  db "touch them to",0
HelpText5:
  db "finish the job.",0

HelpText6:
  db "Complete each",0
HelpText7:
  db "level's goal.",0

HelpText8:
  db "+:Move A:Jump",0
HelpText9:
  db "B:Toss block",0

; ---------------------------------------------------

ClearAndAddFHBGText:
  call ClearNametable
  ld hl, FHBGText
  ld de, SCREEN1+8+1*32
  jp strcpy

FHBGText:
  db "FHBG",0

; ---------------------------------------------------

ShowDie:
  call ClearOAM

  ; Set up all the stars for the explosion
  ld hl, sprites
.init_loop:
  ldh a, [CameraPY]
  ld b, a
  ldh a, [PlayerPY]
  sub b
  add 8
  ld [hl+], a

  ldh a, [CameraPX]
  ld b, a
  ldh a, [PlayerPX]
  sub b
  add 4
  ld [hl+], a

  ld a, $20
  ld [hl+], a
  xor a
  ld [hl+], a
  ld a, L
  cp 4*16
  jr nz, .init_loop

  ld a, 30
  ldh [temp], a

  ; Main loop
.wait:
  call ScreenOn
  call wait_vblank
  ld a, sprites>>8
  call run_dma

  ; Update stars
  ld hl, sprites
  ld bc, .tiny_cosine
  ld de, .tiny_sine
.update_loop:
  ld a, [de]
  inc de
  add [hl]
  ld [hl+], a

  ld a, [bc]
  inc bc
  add [hl]
  ld [hl+], a
  inc l
  inc l

  ld a, L
  cp 4*16
  jr nz, .update_loop


  ldh a, [temp]
  dec a
  ldh [temp], a
  jr nz, .wait

  ldh a, [IsEditMode]
  or a
  jp nz, ShowEditorFromLevel

  call wait_vblank
  call ScreenOff
  jp MainInit

.tiny_sine:
  db  0,  2,  3,  4
.tiny_cosine:
  db  4,  4,  3,  2
  db  0, -2, -3, -4
  db -4, -4, -3, -2
  db  0,  2,  3,  4

; Shows the pre-level screen with the level preview
ShowPreLevel:
  ; screen is off already
  call ClearNametable
  call ClearAttributes
  call UploadTitlePalette
  call ClearAndWriteOAM
  xor a
  ldh [rSCX], a
  ldh [rSCY], a

  ldh a, [LevelNumber]
  call LoadLevel

  ; Convert the level format to what DrawLevelPreview takes
  ld hl, level_map
.convert_level_loop:

  ld de, SingleBlockList
  ld b, -1            ; counter
.convert_level_find:
  ld a, [de]          ; get a block from the list DrawLevelPreview uses
  inc de              ; move to the next block in the list
  inc b               ; increase the counter (so a match on the first one would be 0)
  cp [hl]             ; a match?
  jr nz, .convert_level_find

  ld h, edit_map>>8   ; switch to edit_map temporarily
  ld [hl], b
  inc l
  ld h, level_map>>8  ; switch back
  jr nz, .convert_level_loop

  call DrawLevelPreview

  ; Display the objective
  ldh a, [IsChipLevel]
  or a
  push af
  ld hl, .ChipText
  jr nz, .WasChipLevel
.NotChipLevel:
  ld hl, .DefeatText
.WasChipLevel:
  ld de, SCREEN1+2+17*32
  call strcpy

  pop af
  jr z, .DontWriteChipCount
  ld hl, SCREEN1+2+8+17*32
  call PrintNybble
.DontWriteChipCount:

  ; Write level number
  ld hl, .LevelText
  ld de, SCREEN1+2
  call strcpy
  ldh a, [LevelNumber]
  inc a ; level 0 displays as level 1
  ld hl, SCREEN1+2+6
  call PrintDecimalByte
 
  ; Wait for an A press
.wait:
  call ScreenOn
  call wait_vblank
  call readkeys
  ldh a, [KeyNew]
  and KEY_A
  jr z, .wait

  call wait_vblank
  call ScreenOff
  jp MainInit

.LevelText:
  db "Level",0
.ChipText:
  db "Collect ## chips",0
.DefeatText:
  db "Defeat enemies!",0

; Print a number from 0 to 99
; Uses a division by 10 so it's slower than PrintNybble
PrintDecimalByte:
  push bc
  ld b, 0

.div10:
  cp 10
  jr c, .done
  sub 10
  inc b
  jr .div10
.done:

  ; Write the two digits
  push af
  ld a, b
  or "0"
  ld [hl+], a
  pop af
  or "0"
  ld [hl+], a

  pop bc
  ret

