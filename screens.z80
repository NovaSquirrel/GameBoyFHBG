; FHBG GB
; Copyright (C) 2018 NovaSquirrel
;
; This program is free software: you can redistribute it and/or
; modify it under the terms of the GNU General Public License as
; published by the Free Software Foundation; either version 3 of the
; License, or (at your option) any later version.
;
; This program is distributed in the hope that it will be useful, but
; WITHOUT ANY WARRANTY; without even the implied warranty of
; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
; General Public License for more details.
;
; You should have received a copy of the GNU General Public License
; along with this program.  If not, see <http://www.gnu.org/licenses/>.
;

SECTION "screens", ROM0

ShowTitle:
  ; Decompress title level
  ld de, title
  call DecompressMap
  call RenderLevel
  call UploadLevelPalette

  ; "Forehead"
  ld hl, TitleText1
  ld de, SCREEN1+2+6*32
  ld c, 8
  call memcpy8
  ; "Block Guy"
  ld hl, TitleText2
  ld de, SCREEN1+3+7*32
  ld c, 9
  call memcpy8

  ; On the Game Boy Color, invert the title
  ldh a, [IsGameBoyColor]
  cp $11
  jr nz, .NotColor
  ; Second VRAM bank
  ld a, 1
  ldh [$ff4f], a
  ; Make the bottom row inverted
  ld hl, SCREEN1+2+6*32
  ld a, 7
  ld c, 10
  call memset8
  ld hl, SCREEN1+2+7*32
  ld c, 10
  call memset8
  ; First VRAM bank
  xor a
  ldh [$ff4f], a
.NotColor:

  call ClearOAM
  call RunPlayer

  ; Clear sprites
  ld a, sprites>>8
  call run_dma

  call ScreenOn

  ; Wait for player to press A
.loop:
  call wait_vblank
  call readkeys
  ldh a, [KeyDown]
  and KEY_A
  jr z, .loop

  call ScreenOff

  call ClearOAM

  jp MainInit

TitleText1:
  db "Forehead"
TitleText2:
  db "Block Guy"
PressStart:
  db "Push Start!"
