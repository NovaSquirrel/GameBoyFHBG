; FHBG GB
; Copyright (C) 2018 NovaSquirrel
;
; This program is free software: you can redistribute it and/or
; modify it under the terms of the GNU General Public License as
; published by the Free Software Foundation; either version 3 of the
; License, or (at your option) any later version.
;
; This program is distributed in the hope that it will be useful, but
; WITHOUT ANY WARRANTY; without even the implied warranty of
; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
; General Public License for more details.
;
; You should have received a copy of the GNU General Public License
; along with this program.  If not, see <http://www.gnu.org/licenses/>.
;

SECTION "main", ROM0

main_init:
  ld a, ACTOR_SPINNER
  ld [actors], a
  ld [actors+16], a
  ld [actors+32], a
  ld a, ACTOR_BURGER
  ld [actors+48], a
  ld [actors+64], a

  ld a, 7
  ld [RainbowRGBPIndex], a
  ld a, $80|$3c
  ld [RainbowRGBCIndex], a
  ld a, [RainbowLUT+0]
  ld [RainbowRGBData+0], a
  ld a, [RainbowLUT+1]
  ld [RainbowRGBData+1], a

  call LoadLevel
  call RenderLevel

  call ScreenOn

  ; Erase all initial sprites
  call wait_vblank
  ld a, sprites>>8
  call run_dma

main_loop:
  ; Do graphical updates
  call wait_vblank
  ld a, sprites>>8
  call run_dma
  ldh a, [CameraPX]
  ldh [rSCX], a
  ldh a, [CameraPY]
  ldh [rSCY], a
  call RainbowCopy

  ; Clear sprites
  ld hl, sprites
  xor a
.clear_sprites:
  ld [hl+], a
  inc l
  inc l
  inc l
  jr nz, .clear_sprites

  ; Run game loop stuff
  call readkeys
  xor a
  ldh [oam_ptr], a
  call AdjustCamera
  call RunPlayer
  call RunActors

  jp main_loop
