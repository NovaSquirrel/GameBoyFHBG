; FHBG GB
; Copyright (C) 2018 NovaSquirrel
;
; This program is free software: you can redistribute it and/or
; modify it under the terms of the GNU General Public License as
; published by the Free Software Foundation; either version 3 of the
; License, or (at your option) any later version.
;
; This program is distributed in the hope that it will be useful, but
; WITHOUT ANY WARRANTY; without even the implied warranty of
; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
; General Public License for more details.
;
; You should have received a copy of the GNU General Public License
; along with this program.  If not, see <http://www.gnu.org/licenses/>.
;

SECTION "main", ROM0

MainInit:
  ld a, 4
  ld [PlayerHealth], a
  ld [EnemyLimit], a

  ld a, 7
  ld [RainbowRGBPIndex], a
  ld a, $80|$3c
  ld [RainbowRGBCIndex], a
  ld a, [RainbowLUT+0]
  ld [RainbowRGBData+0], a
  ld a, [RainbowLUT+1]
  ld [RainbowRGBData+1], a

  call LoadLevel
  call RenderLevel
  call UploadLevelPalette

  call ScreenOn

  ; Erase all initial sprites
  call wait_vblank
  ld a, sprites>>8
  call run_dma

MainLoop:
  ; Do graphical updates
  call wait_vblank
  ld a, sprites>>8
  call run_dma
  ldh a, [CameraPX]
  ldh [rSCX], a
  ldh a, [CameraPY]
  ldh [rSCY], a
  call RainbowCopy

  ; Clear sprites
  ld hl, sprites
  xor a
.clear_sprites:
  ld [hl+], a
  inc l
  inc l
  inc l
  jr nz, .clear_sprites

  ; Run game loop stuff
  call readkeys
  xor a
  ldh [oam_ptr], a
  call AdjustCamera
  call RunPlayer
  call RunActors

  ; Are there more enemies to spawn in?
  ldh a, [retraces]
  and 63
  jr nz, .no_spawn_enemy
  ld hl, EnemyLimit
  ld a, [EnemyCount]
  cp [hl]
  jr nc, .no_spawn_enemy

  ldh a, [EnemyList+0]
  ld e, a
  ldh a, [EnemyList+1]
  ld d, a
  ld a, [de]
  or a
  jr nz, .not_end_of_list

  ; If it's not a chip level, stop spawning enemies
  ldh a, [IsChipLevel]
  or a
  jr z, .no_spawn_enemy

  ; If it is, move to the start of the list
  ldh a, [EnemyListStart+0]
  ld e, a
  ldh a, [EnemyListStart+1]
  ld d, a
.not_end_of_list:

  call EnemyFindFree
  jr nc, .no_spawn_enemy

  ; Randomly face left or right
  call GetRandomNumber
  and 128
  ld b, a
  ld a, [de] ; Reread enemy type
  or b
  ld [hl+], a

  ; Move up to X position low
  inc l
  inc l
  inc l
  inc l
  ; Set X position 
  ld a, $80
  ld [hl+], a
  ld a, 7
  ld [hl+], a

  ; Write the incremented pointer back
  inc de
  ld a, e
  ldh [EnemyList+0], a
  ld a, d
  ldh [EnemyList+1], a
.no_spawn_enemy:

  ; Display health
  ld h, sprites>>8
  ldh a, [oam_ptr]
  ld l, a
  add 4
  ldh [oam_ptr], a

  ld a, 16+2
  ld [hl+], a
  ld a, 8+2
  ld [hl+], a
  ld a, [PlayerHealth]
  or $80
  ld [hl+], a
  xor a
  ld [hl+], a

  jp MainLoop
