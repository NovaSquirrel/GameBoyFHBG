; FHBG GB
; Copyright (C) 2018 NovaSquirrel
;
; This program is free software: you can redistribute it and/or
; modify it under the terms of the GNU General Public License as
; published by the Free Software Foundation; either version 3 of the
; License, or (at your option) any later version.
;
; This program is distributed in the hope that it will be useful, but
; WITHOUT ANY WARRANTY; without even the implied warranty of
; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
; General Public License for more details.
;
; You should have received a copy of the GNU General Public License
; along with this program.  If not, see <http://www.gnu.org/licenses/>.
;

SECTION "editor", ROM0

ShowEditorFromLevel:
  call wait_vblank
  call ScreenOff
  call UploadTitlePalette
  call ClearAndWriteOAM
  xor a
  ldh [rSCX], a
  ldh [rSCY], a
ShowEditor:
  call ClearNametable
  call ClearAttributes

  ; Initialize the level if we're entering here from the menu
  ldh a, [IsEditMode]
  or a
  jr nz, .SkipClearMap

  ; Init the level parameters
  xor a
  ldh [IsChipLevel], a
  ld a, 4
  ld [EnemyLimit], a

  ld hl, edit_map
  ld bc, 256+32+1 ; clear enemy list too
  call memclear
  ld a, ACTOR_PLODDER
  ld [edit_enemy_list], a

  xor a
  ldh [CursorX], a
  ldh [CursorY], a
  ld a, 1
  ldh [CursorBrush], a
.SkipClearMap:
  ld a, 1
  ldh [IsEditMode], a

  call DrawLevelPreview

  ; Draw list of blocks on the side
  ld hl, SCREEN1+32
  ld de, 32
  ld a, $11
.draw_side_loop:
  ld [hl], a 
  add hl, de
  inc a
  cp $10|LVL_POLE+1
  jr nz, .draw_side_loop


EditorLoop:
  ; Draw the cursor sprite
  ldh a, [CursorY]
  add a ; * 8
  add a
  add a
  add (1*8)+16
  ld [$fe00], a ; Y position

  ldh a, [CursorX]
  add a ; * 8
  add a
  add a
  add (2*8)+8
  ld [$fe01], a ; X position

  ld a, $20
  ld [$fe02], a ; star
; -----
  ldh a, [CursorBrush]
  add a
  add a
  add a
  add 16
  ld [$fe00+4], a ;Y position
  ld a, 8+8
  ld [$fe01+4], a ; X position
  ld a, $21
  ld [$fe02+4], a ; tile
  ld a, OAM_XFLIP
  ld [$fe03+4], a ; attribute

; ---------------------------------
  call ScreenOn
  call wait_vblank
  call readkeys
  call DoKeyRepeat
  ; Make it easier to draw lines
  ld a, [KeyNew]
  and KEY_LEFT | KEY_DOWN | KEY_UP | KEY_RIGHT
  jr z, .notline
  ld a, [KeyDown]
  and KEY_A
  jr z, .notline
  ; Retrigger the toggle button
  ld a, [KeyNew]
  or KEY_A
  ld [KeyNew], a
.notline:

  ; React to key inputs
  ldh a, [KeyNew]
  ld b, a

  ldh a, [KeyDown]
  and KEY_B
  jr z, .NotB

  bit 7, b
  jr z, .NotBDown
  ldh a, [CursorBrush]
  inc a
  cp LVL_POLE+1
  jr c, .NoBDownCorrect
  ld a, 1
.NoBDownCorrect:
  ldh [CursorBrush], a
.NotBDown:

  bit 6, b
  jr z, .NotBUp
  ldh a, [CursorBrush]
  dec a
  jr nz, .NoBUpCorrect
  ld a, LVL_POLE
.NoBUpCorrect:
  ldh [CursorBrush], a
.NotBUp:

  jr .SkipButtons
.NotB:

  bit 7, b
  jr z, .NotDown
  ldh a, [CursorY]
  inc a
  and 15
  ldh [CursorY], a
.NotDown:

  bit 6, b
  jr z, .NotUp
  ldh a, [CursorY]
  dec a
  and 15
  ldh [CursorY], a
.NotUp:

  bit 5, b
  jr z, .NotLeft
  ldh a, [CursorX]
  dec a
  and 15
  ldh [CursorX], a
.NotLeft:

  bit 4, b
  jr z, .NotRight
  ldh a, [CursorX]
  inc a
  and 15
  ldh [CursorX], a
.NotRight:

  bit 0, b
  jr z, .NotA
  ; Calculate level position
  ldh a, [CursorY]
  add a
  add a
  add a
  add a
  ld c, a
  ldh a, [CursorX]
  or c
  ld c, a
  ld b, edit_map>>8

  ; Replace?
  ld a, [bc]
  or a
  jr z, .FillTile

.EraseTile:
  xor a
  ld [bc], a
  jr .RedrawTile
.FillTile:
  ldh a, [CursorBrush]
  ld [bc], a

.RedrawTile:
  ; Calculate screen position
  ldh a, [CursorY]
  inc a
  ld l, a
  ld h, 0
  add hl, hl
  add hl, hl
  add hl, hl
  add hl, hl
  add hl, hl
  ldh a, [CursorX]
  add 2
  add_hl_a

  ld de, SCREEN1
  add hl, de
  call wait_vblank
  ld a, [bc]
  add $10
  ld [hl], a
.NotA:

.SkipButtons:

  ldh a, [KeyNew]
  and KEY_START
  jp z, EditorLoop

  ; falls into ShowEditorMenu
ShowEditorMenu:
  call wait_vblank
  call ScreenOff
;  call UploadTitlePalette
  call ClearAndWriteOAM

  call ClearAndAddFHBGText
  call ClearAttributes

  xor a ; first option
  ldh [temp], a

  ld hl, EditTestText
  ld de, SCREEN1+3+5*32
  call strcpy
  ld hl, EditEnemiesText
  ld de, SCREEN1+3+7*32
  call strcpy
  ld hl, EditNeededChips
  ld de, SCREEN1+3+9*32
  call strcpy
  ld hl, EditEnemyLimit
  ld de, SCREEN1+3+11*32
  call strcpy
  ld hl, BackToMainText
  ld de, SCREEN1+3+13*32
  call strcpy

EditorMenuLoop:
  ; Draw the cursor sprite
  ldh a, [temp]
  add a ; * 16
  add a
  add a
  add a
  add (5*8)+16
  ld [$fe00], a ; Y position

  ld a, 8*2+8
  ld [$fe01], a ; X position

  ld a, $20
  ld [$fe02], a ; star

  ld hl, $9930
  ldh a, [IsChipLevel]
  call PrintNybble

  ld hl, $9970
  ld a, [EnemyLimit]
  call PrintNybble

  call ScreenOn
  call wait_vblank
  call readkeys

  ; React to key inputs
  ldh a, [KeyNew]
  ld b, a

  bit 7, b
  jr z, .NotDown
  ldh a, [temp]
  inc a
  ldh [temp], a
  cp 5
  jr nz, .NotDown
  xor a
  ldh [temp], a
.NotDown:

  bit 6, b
  jr z, .NotUp
  ldh a, [temp]
  dec a
  ldh [temp], a
  rla
  jr nc, .NotUp
  ld a, 4
  ldh [temp], a
.NotUp:

  bit 5, b
  jr z, .NotLeft
  ; Select a parameter to decrease
  ldh a, [temp]
  sub 2
  jr z, .NeededChipLeft
  dec a
  jr nz, .NotLeft
.EnemyLimitLeft:
  ld a, [EnemyLimit]
  dec a
  and 15
  ld [EnemyLimit], a
  jr .NotLeft
.NeededChipLeft:
  ldh a, [IsChipLevel]
  dec a
  and 15
  ldh [IsChipLevel], a
.NotLeft:

  bit 4, b
  jr z, .NotRight
  ; Select a parameter to increase
  ldh a, [temp]
  sub 2
  jr z, .NeededChipRight
  dec a
  jr nz, .NotRight
.EnemyLimitRight:
  ld a, [EnemyLimit]
  inc a
  and 15
  ld [EnemyLimit], a
  jr .NotRight
.NeededChipRight:
  ldh a, [IsChipLevel]
  inc a
  and 15
  ldh [IsChipLevel], a
.NotRight:

  bit 0, b
  jr z, .NotA
  ldh a, [temp]
  or a
  jp z, EditorTestLevel
  dec a
  jp z, ShowEditorEnemyList
  sub 3 ; skip the two options that aren't choosable
  jp z, ShowMainMenuFromEditor
.NotA:

  bit 1, b ; B, cancel the menu
  jp nz, ShowEditorFromLevel

  jp EditorMenuLoop

EditTestText:
  db "Test level",0
EditEnemiesText:
  db "Enemy list",0
EditNeededChips:
  db "Needed chips:",0
EditEnemyLimit:
  db "Enemy limit:",0
BackToMainText:
  db "Exit editor",0

ShowEditorEnemyList:
  call wait_vblank
  call ScreenOff
  call ClearAndWriteOAM
  call ClearAndAddFHBGText

  ld hl, EditorEnemyListAdd
  ld de, SCREEN1+4+11*32
  call strcpy
  ld hl, EditorEnemyListRemove
  ld de, SCREEN1+4+13*32
  call strcpy
  ld hl, EditorEnemyListExit
  ld de, SCREEN1+4+15*32
  call strcpy

  ; First item
  xor a
  ldh [temp], a

ShowEditorEnemyListLoop:
  call ClearOAM
  ; Draw the cursor sprite
  ld a, (8*8)+16
  ld [sprites+0], a ; Y position

  ld a, (8*9)+6+8
  ld [sprites+1], a ; X position

  ld a, $20
  ld [sprites+2], a ; star

  ; Draw the list of enemy sprites
  ld c, 0           ; start count at 0

  ; Calculate starting position for the pointer
  ld hl, edit_enemy_list
  ldh a, [temp]
  sub 2             ; Start 2 enemies behind, so the cursor is in the middle
  push af
  add_hl_a
  pop af
  ; Negative?
  rla
  jr nc, .not_negative
  
  ldh a, [temp]
  ld b, a
  ld a, 2
  sub b
  ld c, a
  ld hl, edit_enemy_list
.not_negative:


  ld de, sprites+4
.display_loop:
  ; Get the tiles for the current enemy
  ld a, [hl+]
  or a
  jr z, .exit_display_loop
  push hl
  sub ACTOR_PLODDER
  ld hl, EditorEnemyTiles
  add_hl_a
  ld a, [hl]
  ldh [temp+2], a
  pop hl

  ; LEFT -----------------------------
  ; Y position
  ld a, (8*6)+16
  ld [de], a
  inc e

  ; X position
  ld a, c
  add a
  add a
  add a
  add a
  add a
  add 10+8
  ldh [temp+1], a
  ld [de], a
  inc e

  ; Tile
  ldh a, [temp+2]
  ld [de], a
  inc e

  ; Attributes
  xor a
  ld [de], a
  inc e

  ; RIGHT -----------------------------
  ; Y position
  ld a, (8*6)+16
  ld [de], a
  inc e

  ; X position
  ldh a, [temp+1]
  add 8
  ld [de], a
  inc e

  ; Tile
  ldh a, [temp+2]
  add 2
  ld [de], a
  inc e

  ; Attributes
  xor a
  ld [de], a
  inc e

  ;-----------------
  ; Next entry
  ld a, l
  cp 32
  jr z, .exit_display_loop

  inc c
  ld a, c
  cp 5
  jr nz, .display_loop
.exit_display_loop:


  call ScreenOn
  call wait_vblank
  ld a, sprites>>8
  call run_dma
  call readkeys

  ldh a, [KeyNew]
  ld b, a

  bit 5, b
  jr z, .NotLeft
  ldh a, [temp]
  dec a
  and 31
  ldh [temp], a
  ; Keep the cursor within the list
  call .GetCurrentPositionPointer
  jr nz, .NotLeft
  ; Loop around to the last item in the list
.FindLast:
  ld a, [hl-]
  or a
  jr z, .FindLast
  inc l
  ld a, l
  ldh [temp], a
.NotLeft:

  bit 4, b
  jr z, .NotRight
  ldh a, [temp]
  inc a
  and 31
  ldh [temp], a
  ; Keep the cursor within the list
  call .GetCurrentPositionPointer
  jr nz, .NotRight
  xor a
  ld [temp], a
.NotRight:

  call .GetCurrentPositionPointer

  bit 6, b
  jr z, .NotUp
  inc [hl]
  cp ACTOR_NUMBER_LEFT-1
  jr nz, .NotUp
  ld [hl], ACTOR_PLODDER
.NotUp:

  bit 7, b
  jr z, .NotDown
  dec [hl]
  cp ACTOR_PLODDER
  jr nz, .NotDown
  ld [hl], ACTOR_NUMBER_LEFT-1
.NotDown:

  ; A = duplicate
  bit 0, b
  jr z, .NotA
  ldh a, [temp]
  cp 31
  jr z, .NotA
  ; Calculate how many items to move
  ld c, a
  ld a, 31
  sub c
  ld c, a
  ld hl, edit_enemy_list+30
  ld de, edit_enemy_list+31
.DupeLoop:
  ld a, [hl-]
  ld [de], a
  dec e
  dec c
  jr nz, .DupeLoop
.NotA:

  ; B = delete
  bit 1, b
  jr z, .NotB
  ; Can't delete if only item
  ld a, [edit_enemy_list+1]
  or a
  jr z, .NotB
  ; Get the pointers
  call .GetCurrentPositionPointer
  ld d, h
  ld e, l
  inc de
.DeleteLoop:
  ld a, [de]
  inc e
  ld [hl+], a

  ld a, l
  cp 32
  jr nz, .DeleteLoop
  
  ; Delete last item
  xor a
  ld [edit_enemy_list+31], a

  ; If we delete the last item, move back
  call .GetCurrentPositionPointer
  jr nz, .NotB
  ldh a, [temp]
  dec a
  ldh [temp], a
.NotB:

  bit 3, b
  jp z, ShowEditorEnemyListLoop
  jp ShowEditorMenu

.GetCurrentPositionPointer:
  ld hl, edit_enemy_list
  ldh a, [temp]
  add_hl_a
  ld a, [hl]
  or a
  ret


EditorEnemyListAdd:
  db "A: Duplicate",0
EditorEnemyListRemove:
  db "B: Remove",0
EditorEnemyListExit:
  db "Start: Exit",0


EditorTestLevel:
  call wait_vblank
  call ScreenOff
; Test the level
  ld a, low(edit_enemy_list)
  ldh [EnemyList+0], a
  ldh [EnemyListStart+0], a
  ld a, high(edit_enemy_list)
  ldh [EnemyList+1], a
  ldh [EnemyListStart+1], a
  ldh a, [CursorX]
  ldh [PlayerPXH], a
  ldh a, [CursorY]
  ldh [PlayerPYH], a
  xor a
  ldh [PlayerPXL], a
  ldh [PlayerPYL], a

  ; Convert the level from editor IDs to metatile IDs
  ld hl, edit_map
  ld de, level_map
ConvertLevelLoop:
  ; Get the converted level block
  ld a, [hl+]
  push hl
  ld hl, SingleBlockList
  add_hl_a
  ld a, [hl]
  pop hl

  ld [de], a
  inc e
  jr nz, ConvertLevelLoop
  jp MainInit

DrawLevelPreview:
  ld hl, SCREEN1+2+32
  ld de, edit_map
.loop:
  ; Write one tile
  ld a, [de]
  inc de
  or 16
  ld [hl+], a

  ; Stop if at end of level
  ld a, e
  or a
  ret z
  ; New row?
  and $0f
  jr nz, .loop
  ; New row, adjust pointer
  ld bc, 16
  add hl, bc
  jr .loop

PrintNybble:
  cp 10
  jr c, .not_ten
  ld [hl], "1"
  inc l
  sub 10
  jr .was_ten
.not_ten
  ld [hl], "0"
  inc l
.was_ten:

  add "0"
  ld [hl], a
  ret

EditorEnemyTiles:
  db $50 ; plodder
  db $40 ; sneaker
  db $78 ; spinner
  db $58 ; owl
  db $70 ; george
  db $48 ; burger
  db $28 ; fire walk
  db $30 ; fire jump
  db $60 ; ball guy
  db $68 ; ball roll
  db $64 ; thwomp
  db $6c ; mine
